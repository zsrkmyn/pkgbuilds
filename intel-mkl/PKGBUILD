# Maintainer: Stephen Zhang <zsrkmyn at gmail dot com>
pkgname=intel-mkl
pkgver=2017.0.098
pkgrel=2
pkgdesc="Intel(R) Math Kernel Libray"
arch=('x86_64')
url="https://software.intel.com/en-us/intel-mkl"
license=('custom')
source=("http://registrationcenter-download.intel.com/akdlm/irc_nas/tec/9662/l_mkl_$pkgver.tgz"\
        "intel-mkl.conf"
        "intel-mkl.sh")
md5sums=('3cdcb739ab5ab1e047eb130b9ffdd8d0'
         '9e9a761dc9cfb598b66baf409c087ff4'
         'f2b6e23e4f2c7a0cd26ac14b920fcd94')
options=(staticlibs)

build() {
	cd "l_mkl_$pkgver"
	rm -rf build && mkdir build && cd build
	for i in ../rpm/*x86_64.rpm ../rpm/*noarch.rpm; do
		bsdtar -xf $i
	done
}

package() {
	install -dm 755 "$pkgdir/usr/share/licenses/intel-mkl"
	install -dm 755 "$pkgdir/etc/profile.d/"
	install -dm 755 "$pkgdir/etc/ld.so.conf.d/"
	install -Dm 755 intel-mkl.sh "$pkgdir/etc/profile.d/"
	install -Dm 644 intel-mkl.conf "$pkgdir/etc/ld.so.conf.d/"

	cd "l_mkl_$pkgver/build"
	install -Dm 644 ../license.txt "$pkgdir/usr/share/licenses/intel-mkl"

	cd opt/intel/compilers_and_libraries_$pkgver/linux/
	rm -r \
		mkl/benchmarks/ mkl/lib/*mic mkl/.marker* tbb/lib/ia32{,_lin} \
		compiler/lib/intel64_lin_mic/ bin/
	cd -

	mv opt/intel/compilers_and_libraries_$pkgver/linux/* opt/intel/
	rm -r opt/intel/compilers_and_libraries_$pkgver/

	rm -r opt/intel/parallel_studio_xe_2017.0.035/

	sed -i 's#<INSTALLDIR>#/opt/intel#' opt/intel/mkl/bin/*
	mv opt "$pkgdir"
}
